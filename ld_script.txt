gNumMusicPlayers = 4;
gMaxLines = 80;
SECTIONS {
    . = 0x2000000;
    ewram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_ewram.txt"
    }
    . = 0x3000000;
    iwram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_iwram.txt"
    }
    . = 0x8000000;
    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        src/main.o(.text);
        src/bg3_control_1.o(.text);
        asm/bg3_control_2.o(.text); /* merge files later, sorry. by Pidgey */
        src/bg3_control_3.o(.text);
        src/bg_control.o(.text);
        src/utils.o(.text);
        src/rom8002D6C.o(.text);
        asm/rom8002D6C.o(.text);
        src/script_main.o(.text);
        src/script_commands.o(.text);
        src/script_commands2.o(.text);
        src/start_processes.o(.text);
        src/save_processes.o(.text);
        src/rom8008DF4.o(.text);
        asm/rom8008DF4.o(.text);
        asm/continue_process.o(.text);
        src/process3.o(.text);
        src/process5.o(.text);
        src/process6.o(.text);
        asm/process9.o(.text);
        src/process4.o(.text);
        asm/process4.o(.text);
        src/rom800D674.o(.text);
        src/court_record.o(.text);
        src/sound_control.o(.text);
        src/animation.o(.text);
        asm/m4a_1.o(.text);
        src/m4a.o(.text);
        asm/libagbsyscall.o(.text);
        src/agb_sram.o(.text);
        *libgcc.a:*_call_via_rX*.o(.text*);
        *libgcc.a:*_divsi3*.o(.text*);
        *libgcc.a:*_dvmd_tls*.o(.text*);
        *libgcc.a:*_udivsi3*.o(.text*);
        *libgcc.a:*_umodsi3*.o(.text*);
    } =0
    .rodata :
    ALIGN(4)
    {
        src/main.o(.rodata);
        src/bg3_control_1.o(.rodata);
        data/data.o(.rodata);
        data/sound_data.o(.rodata);
        src/m4a_tables.o(.rodata);
        data/sound_data2.o(.rodata);
        src/agb_sram.o(.data); 
    } =0
    /* capcom :^)) */
    . += 0x100;
    .data :
    ALIGN(4)
    {
        data/data_3.o(.data); /* these are all pointers */
    } =0
    . = 0x8180000;
    gfx_data :
    ALIGN(4)
    {
        data/graphics.o(.data);
    } =0

    script_data :
    ALIGN(4)
    {
        data/scripts.o(.data);
    } =0

    /* DWARF debug sections.
    Symbols in the DWARF debugging sections are relative to the beginning
    of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    
	/DISCARD/ :
    {
        *(*);
    }
	
}