gNumMusicPlayers = 4;
gMaxLines = 80;
SECTIONS {
    . = 0x2000000;
    ewram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_ewram.txt"
    }
    . = 0x3000000;
    iwram (NOLOAD) :
    ALIGN(4)
    {
        INCLUDE "sym_iwram.txt"
    }
    . = 0x8000000;
    .text :
    ALIGN(4)
    {
        src/crt0.o(.text);
        /* main.c */
        src/main.o(.text);
        /* bg.c */
        src/bg3.o(.text);
        src/bg.o(.text);
        /* sub1.c + sub2.c */
        src/utils.o(.text);
        /* scenario.c */
        src/case_data.o(.text);
        /* sce1_0.c */
        src/investigation_segment_1_0.o(.text);
        /* sce1_2.c */
        src/investigation_segment_1_2.o(.text);
        /* sce2_0.c */
        src/investigation_segment_2_0.o(.text);
        /* sce2_2.c */
        src/investigation_segment_2_2.o(.text);
        /* sce2_4.c */
        src/investigation_segment_2_4.o(.text);
        /* sce3_0.c */
        src/investigation_segment_3_0.o(.text);
        /* sce3_2.c */
        src/investigation_segment_3_2.o(.text);
        /* sce3_4.c */
        src/investigation_segment_3_4.o(.text);
        /* mess.c */
        src/script_main.o(.text);
        /* mes_sub0.c */
        src/script_commands.o(.text);
        /* mes_sub1.c */
        src/script_commands2.o(.text);
        /* mes_sub2.c */
        src/script_commands3.o(.text);
        /* title.c */
        src/title_processes.o(.text);
        /* gameover.c */
        src/gameover_process.o(.text);
        /* saveload.c */
        src/save_processes.o(.text);
        src/episode_load_processes.o(.text);
        /* saiban.c */
        src/court.o(.text);
        /* tantei.c */
        src/investigation.o(.text);
        /* status.c */
        src/court_record.o(.text);
        /* sound.o */
        src/sound.o(.text);
        /* obj_sub.c */
        src/animation.o(.text);
        /* library functions */
        src/m4a_1.o(.text);
        src/m4a.o(.text);
        src/libagbsyscall.o(.text);
        src/agb_sram.o(.text);
        src/vwf.o(.text);
        *libgcc.a:*_call_via_rX*.o(.text*);
        *libgcc.a:*_divsi3*.o(.text*);
        *libgcc.a:*_dvmd_tls*.o(.text*);
        *libgcc.a:*_udivsi3*.o(.text*);
        *libgcc.a:*_umodsi3*.o(.text*);
    } =0
    .rodata :
    ALIGN(4)
    {
        src/vwf.o(.rodata);
        src/main.o(.rodata);
        src/bg3.o(.rodata);
        src/bg.o(.rodata);
        src/utils.o(.rodata);
        src/case_data.o(.rodata);
        /* sce1_0.c */
        src/investigation_segment_1_0.o(.rodata);
        /* sce1_2.c */
        src/investigation_segment_1_2.o(.rodata);
        /* sce2_0.c */
        src/investigation_segment_2_0.o(.rodata);
        /* sce2_2.c */
        src/investigation_segment_2_2.o(.rodata);
        /* sce2_4.c */
        src/investigation_segment_2_4.o(.rodata);
        /* sce3_0.c */
        src/investigation_segment_3_0.o(.rodata);
        /* sce3_2.c */
        src/investigation_segment_3_2.o(.rodata);
        /* sce3_4.c */
        src/investigation_segment_3_4.o(.rodata);
        src/script_main.o(.rodata);
        src/script_commands.o(.rodata);
        src/script_commands2.o(.rodata);
        src/gameover_process.o(.rodata);
        src/save_processes.o(.rodata);
        src/court_record.o(.rodata);
        src/animation.o(.rodata);
        data/sound_data.o(.rodata);
        src/m4a_tables.o(.rodata);
        data/sound_data2.o(.rodata);
        src/agb_sram.o(.rodata); 
    } =0
    /* capcom :^)) */
    . += 0x100;
    .data :
    ALIGN(4)
    {
        src/main.o(.data);
        src/bg.o(.data);
        src/utils.o(.data);
        src/case_data.o(.data);
        src/script_main.o(.data);
        src/save_processes.o(.data);
        src/court.o(.data);
        src/investigation.o(.data);
        src/court_record.o(.data);
        src/animation.o(.data);
    } =0
    . = 0x8180000;
    gfx_data :
    ALIGN(4)
    {
        data/graphics.o(gfx_data);
    } =0

    script_data :
    ALIGN(4)
    {
        src/scripts.o(script_data);
    } =0

    /* DWARF debug sections.
    Symbols in the DWARF debugging sections are relative to the beginning
    of the section so we begin them at 0.  */

    /* DWARF 1 */
    .debug          0 : { *(.debug) }
    .line           0 : { *(.line) }

    /* GNU DWARF 1 extensions */
    .debug_srcinfo  0 : { *(.debug_srcinfo) }
    .debug_sfnames  0 : { *(.debug_sfnames) }

    /* DWARF 1.1 and DWARF 2 */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }

    /* DWARF 2 */
    .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    
	/DISCARD/ :
    {
        *(*);
    }
	
}